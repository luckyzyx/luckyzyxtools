plugins {
    id 'com.android.application'
}

android {
    signingConfigs {
        release {
            storeFile file('D:\\Android\\luckyzyx\\luckyzyx.jks')
            storePassword 'luckyzyx'
            keyAlias 'luckyzyx'
            keyPassword 'luckyzyx'
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    def currentVersionCode = getVersionCode()
    defaultConfig {
        applicationId "com.luckyzyx.tools"
        versionCode currentVersionCode
        versionName '2.0'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.release
        minSdkVersion 30
        targetSdk 31
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true     //源代码混淆压缩
            shrinkResources true     //移除无用的resource文件
            zipAlignEnabled true     //zipAlign优化
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    compileSdk 31
    applicationVariants.all { variant ->
        variant.outputs.all {
            if ('release' == variant.buildType.name) {
                outputFileName = "luckyzyx_v${variant.versionName}.apk"
            }
            if ('debug' == variant.buildType.name) {
                outputFileName = "luckyzyx_v${variant.versionName}_debug.apk"
            }
        }
    }
//    allprojects {
//        gradle.projectsEvaluated {
//            tasks.withType(JavaCompile) {
//                options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
//            }
//        }
//    }

}

dependencies {
    //Xposed基础
    compileOnly "de.robv.android.xposed:api:82"
    //使能在平台的旧版API上访问新API   原V7包
//    implementation 'androidx.appcompat:appcompat:1.4.0'
    //Material主题
    implementation 'com.google.android.material:material:1.4.0'
    //约束布局
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    //百分比布局
    implementation 'androidx.percentlayout:percentlayout:1.0.0'
    //快速创建settings
    implementation 'androidx.preference:preference:1.1.1'
    //原 v4包
//    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

def getVersionCode() {
    def propsFile = file('version.properties')
    def properties = new Properties()
    properties.load(new FileInputStream(propsFile))
    def vCode = properties['versionCode'].toInteger()
    properties['versionCode'] = (++vCode).toString()
    properties.store(propsFile.newWriter(), null)
    return vCode
}